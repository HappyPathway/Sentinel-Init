import "tfplan/v2" as tfplan

allContainerClusters = filter tfplan.resource_changes as _, resource_changes {
	resource_changes.type is "google_container_cluster" and
		resource_changes.mode is "managed" and
		(resource_changes.change.actions contains "create" or
			resource_changes.change.actions is ["update"])
}

print("CIS 7.11: Ensure Network policy is enabled on Kubernetes Engine Clusters")

deny_undefined_network_policy = rule {
	all allContainerClusters as _, cluster {
		keys(cluster.change.after) contains "network_policy"
	}
}

network_policy_is_enabled = rule when deny_undefined_network_policy is true {
	all allContainerClusters as _, cluster {
		all cluster.change.after.network_policy as _, network_policy {
			network_policy.enabled is true
		}
	}
}

main = rule {
	deny_undefined_network_policy and
	network_policy_is_enabled
}
