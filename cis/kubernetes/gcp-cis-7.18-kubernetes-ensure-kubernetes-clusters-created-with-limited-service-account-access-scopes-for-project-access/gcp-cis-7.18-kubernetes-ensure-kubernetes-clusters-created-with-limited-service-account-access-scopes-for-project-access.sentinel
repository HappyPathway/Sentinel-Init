import "tfplan/v2" as tfplan

supportedAccessScopes = [
	"https://www.googleapis.com/auth/logging.write",
	"https://www.googleapis.com/auth/monitoring",
	"https://www.googleapis.com/auth/devstorage.read_only",
	"storage-ro",
	"logging-write",
	"monitoring",
]

allContainerClusters = filter tfplan.resource_changes as _, resource_changes {
	resource_changes.type is "google_container_cluster" and
		resource_changes.mode is "managed" and
		(resource_changes.change.actions contains "create" or
			resource_changes.change.actions is ["update"])
}

allNodePools = filter tfplan.resource_changes as _, resource_changes {
	resource_changes.type is "google_container_node_pool" and
		resource_changes.mode is "managed" and
		(resource_changes.change.actions contains "create" or
			resource_changes.change.actions is ["update"])
}

print("CIS 7.18: Ensure Kubernetes Clusters created with limited service account Access scopes for Project access")

deny_undefined_cluster_node_config = rule {
	all allContainerClusters as _, cluster {
		keys(cluster.change.after) contains "node_config"
	}
}

cluster_supported_access_scope_is_configured = rule when deny_undefined_cluster_node_config is true {
	all allContainerClusters as _, cluster {
		all cluster.change.after.node_config as _, node_config {
			all node_config.oauth_scopes as oauth_scopes {
				oauth_scopes in supportedAccessScopes
			}
		}
	}
}

deny_undefined_pool_node_config = rule {
	all allNodePools as _, pool {
		keys(pool.change.after) contains "node_config"
	}
}

pool_supported_access_scope_is_configured = rule when deny_undefined_pool_node_config is true {
	all allNodePools as _, pool {
		all pool.change.after.node_config as _, node_config {
			all node_config.oauth_scopes as oauth_scopes {
				oauth_scopes in supportedAccessScopes
			}
		}
	}
}

main = rule {
	deny_undefined_cluster_node_config and
	deny_undefined_pool_node_config and
	cluster_supported_access_scope_is_configured and
	pool_supported_access_scope_is_configured
}
