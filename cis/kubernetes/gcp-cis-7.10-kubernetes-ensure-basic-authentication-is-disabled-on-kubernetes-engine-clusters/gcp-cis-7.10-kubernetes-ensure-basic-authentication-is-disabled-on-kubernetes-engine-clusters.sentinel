import "tfplan/v2" as tfplan

allContainerClusters = filter tfplan.resource_changes as _, resource_changes {
	resource_changes.type is "google_container_cluster" and
		resource_changes.mode is "managed" and
		(resource_changes.change.actions contains "create" or
			resource_changes.change.actions is ["update"])
}

print("CIS 7.10: Ensure Basic Authentication is disabled on Kubernetes Engine Clusters")

master_auth_is_defined = rule {
	all allContainerClusters as _, cluster {
		keys(cluster.change.after) contains "master_auth"
	}
}

basic_authentication_is_disabled = rule when master_auth_is_defined is true {
	all allContainerClusters as _, cluster {
		all cluster.change.after.master_auth as _, master_auth {
			keys(master_auth) contains "username" and
				length(master_auth.username) == 0 and
				keys(master_auth) contains "password" and
				length(master_auth.password) == 0
		}
	}
}

main = rule {
	master_auth_is_defined and
	basic_authentication_is_disabled
}
