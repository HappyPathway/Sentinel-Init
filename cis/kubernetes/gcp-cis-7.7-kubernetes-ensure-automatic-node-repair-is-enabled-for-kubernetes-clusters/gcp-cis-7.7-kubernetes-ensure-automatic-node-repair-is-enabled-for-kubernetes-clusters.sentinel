import "tfplan/v2" as tfplan

allNodePools = filter tfplan.resource_changes as _, resource_changes {
	resource_changes.type is "google_container_node_pool" and
		resource_changes.mode is "managed" and
		(resource_changes.change.actions contains "create" or
			resource_changes.change.actions is ["update"])
}

print("CIS 7.7: Ensure `Automatic node repair` is enabled for Kubernetes Clusters")

deny_undefined_management = rule {
	all allNodePools as _, pool {
		keys(pool.change.after) contains "management"
	}
}

node_auto_repair_is_enabled = rule when deny_undefined_management is true {
	all allNodePools as _, pool {
		all pool.change.after.management as _, management {
			management.auto_repair is true
		}
	}
}

main = rule {
	deny_undefined_management and
	node_auto_repair_is_enabled
}
